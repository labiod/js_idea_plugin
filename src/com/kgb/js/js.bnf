{
    parserClass="com.kgb.js.parser.JSParser"
//    parserUtilClass="com.kgb.lisp.parser.LispParserUtil"

    psiClassPrefix="JS"
    psiImplClassSuffix="Impl"
    psiPackage="com.kgb.js.psi"
    psiImplPackage="com.kgb.js.psi.impl"
    psiImplUtilClass="com.kgb.js.psi.impl.JSPsiImplUtil"

    elementTypeHolderClass="com.kgb.js.psi.JSTypes"
    elementTypeClass="com.kgb.js.psi.JSElementType"
    tokenTypeClass="com.kgb.js.psi.JSTokenType"

    tokens=[
        EQ="="
        EQ2="=="
        EQ3="==="
        LP='{'
        RP='}'
        LP2='('
        RP2=')'
        LP3='['
        RP3=']'

        op_1='+'
        op_2='-'
        op_3='*'
        op_4='/'
        op_5='!'
        op_6='>'
        op_7='>='
        op_8='<'
        op_9='<='

        AND='&&'
        OR='||'

        number='regexp:\d+(\.\d*)?'
        string="regexp:('([^'\\]|\\.)*'|\"([^\"\\]|\\.)*\")"
        SEMI=";"
        COMMA=","
        DOT='.'
        INCREMENT='++'
        DECREMENT='--'

        newline = "regexp:\n"
        space='regexp:[\s\t]+'
        comment='regexp://.*'
        multiline_comment='regexp:/\*.*\n\t\r\*/'
        vname='regexp:\p{Alpha}\w*'

        TRUE="true"
        FALSE="false"
        NULL="null"

        // keywords
        var='var'
        function='function'
        if='if'
        else='else'
        for='for'
        return='return'
    ]

    name(".*expr")='expression'
    extends(".*expr")=expr
}

root ::= root_item *

private root_item ::= (function_def | function_call | if_statement | for_statement | return_statement | var_declaration | var_property) end_statement?
private end_statement ::= SEMI

var_declaration ::= var? var_property '=' expr
var_property ::= (function_call | ref_expr) (('.' var_property) | (LP3 (vname | number | string | true | false | null) RP3 ))?

//variable arithmetic
expr ::= (factor plus_expr * ) | function_def | array
left plus_expr ::= plus_op factor
private plus_op ::= op_1 | op_2
private factor ::= primary mul_expr *
left mul_expr ::= mul_op primary
private mul_op ::= op_3 | op_4
private primary ::= primary_inner factorial_expr ?
left factorial_expr ::= op_5
private primary_inner ::= var_property | literal_expr | ref_expr | paren_expr
paren_expr ::= '(' expr ')' {pin=1}
ref_expr ::= vname (LP3 (vname | number | string | true | false | null) RP3 )?
private literal_expr ::= number | string | true | false | null

array ::= LP3 <<comma_separated_list(vname | number | string | true | false | null)>> RP3
//root_item definition
function_def ::= function vname? fun_args LP body RP
function_call ::= vname fun_params
inline_function_def ::= function fun_args LP body RP
private fun_args ::= LP2 <<comma_separated_list(vname)>>? RP2
private fun_params ::= LP2 <<comma_separated_list(vname|string|number|true|false|null)>>? RP2
private body ::= root_item*

if_statement ::= if LP2 SPACE* conditions SPACE* RP2 LP body RP else_if_statement*
else_if_statement ::= (else if SPACE* LP2 SPACE* conditions SPACE* RP2 SPACE* LP body RP) | else_statement
else_statement ::= else SPACE* LP body RP
return_statement ::= return conditions
for_statement ::= for LP2 for_dec SEMI conditions SEMI for_inc RP2 LP body RP
private for_dec ::= <<comma_separated_list(var_declaration)>>
private for_inc ::= <<comma_separated_list(var_inc_dec)>>
private var_inc_dec ::= vname (INCREMENT | DECREMENT)

conditions ::= '!'? (cond_expr | number | true | false | string | var_property)

cond_expr ::= (var_property | string | true | false | null ) (eq_st | lw_st | gr_st) (var_property | string | true | false | null )

private eq_st ::= EQ | EQ2 | EQ3
private gr_st ::= op_6 | op_7
private lw_st ::= op_8 | op_9

meta comma_separated_list ::= <<param>> (',' <<param>>) *
meta semicolon_separated_list ::= <<param>> (';' <<param>>) *
meta and_logic_separated_list ::= <<param>> ('&&' <<param>>) *
meta or_logic_separated_list ::= <<param>> ('||' <<param>>) *
//file_body ::= <<semicolon_separated_list(json_object | var_declaration)>>
//json_object ::= LP SPACE* <<comma_separated_list(json_object | json_item)>> SPACE* RP
//json_item ::= var_declaration


